/** chess_pgn_inpos( +Pgn, -Inpos ).

Inpos are all the long integer positions, chess_dict_inpo/2, generated by a (single) PGN game term.

==
?- pgn( pgn('4ncl_short'), [PgnGame1|_] ), chess_pgn_inpos( PgnGame1, Inpos ).
PgnGame1 = pgn(['Event'-'4NCL Division 1a', 'Site'-'Telford, ENG', 'Date'-'2017.11.11', 'Round'-'1.11', 'White'-'Sadler, Matthew D', 'Black'-'Wheeler, Darren P', 'Result'-'1-0', ... - ...|...], [move(1, e4, e6, '', ''), move(2, d3, d5, '', ''), move(3, 'Nd2', 'Nf6', '', ''), move(4, e5, 'Nfd7', '', ''), move(5, f4, b6, '', ''), move(6, g3, c5, '', ''), move(7, 'Bg2', 'Bb7', '', ''), move(..., ..., ..., ..., ...)|...], '1-0', [[91, 69, 118, 101, 110, 116, 32|...], [91, 83, 105, 116, 101, 32|...], [91, 68, 97, 116, 101|...], [91, 82, 111, 117|...], [91, 87, 104|...], [91, 66|...], [91|...], [...|...]|...]),
Inpos = [100700000000010408070000000001020907000000000103120700000100000611070000000001050907000000000103080700000000010210070000000001003731, 100700000000010408070000000001020907000000000103120007000100000611070000000001050907000000000103080700000000010210070000000001040030|...]

==

@author nicos angelopoulos
@version  0:1 2020/3/27

*/
chess_pgn_inpos( Pgn, Inpos ) :-
    Pgn = pgn(_Info,Moves,_Res,_Text),
    chess_dict_start_board( Start ),
    chess_moves_game_plys( Moves, Game ),
    chess_dict_game_play( Game, Start, Boards ),
    maplist( chess_dict_inpo, Boards, Inpos ).


chess_dict_game_play( [], _Board, [] ).
chess_dict_game_play( [Mv-Turn|T], Board, [Coard|Dicts] ) :-
    chess_dict_move( Mv, Board, Turn, Coard ),
    chess_dict_game_play( T, Coard, Dicts ).

chess_moves_game_plys( [], [] ).
chess_moves_game_plys( [move(_,White,BlackPrv,_,_)|T], [White-0|R] ) :-
    ( BlackPrv == [] ->    % T should be [] too.... fixme:
        R = M
        ;
        R = [BlackPrv-1|M]
    ),
    chess_moves_game_plys( T, M ).
